plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'com.google.dagger.hilt.android'
    id 'com.google.devtools.ksp'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("keystore.properties")
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def signingValue = { String key, String envKey ->
    def prop = keystoreProperties.getProperty(key)
    if (prop != null && !prop.trim().isEmpty()) return prop.trim()
    def env = System.getenv(envKey)
    return (env != null && !env.trim().isEmpty()) ? env.trim() : null
}

android {
    namespace = 'com.jellycine.app'
    compileSdk = 36

    defaultConfig {
        applicationId = "com.jellycine.app"
        minSdk = 27
        targetSdk = 36
        versionCode = rootProject.ext.appVersionCode as Integer
        versionName = rootProject.ext.appVersionName as String

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary = true
        }
    }

    signingConfigs {
        release {
            def storeFilePath = signingValue("storeFile", "JELLYCINE_STORE_FILE") ?: "jellycine-release.keystore"
            def storePasswordValue = signingValue("storePassword", "JELLYCINE_STORE_PASSWORD")
            def keyAliasValue = signingValue("keyAlias", "JELLYCINE_KEY_ALIAS")
            def keyPasswordValue = signingValue("keyPassword", "JELLYCINE_KEY_PASSWORD")

            if (storePasswordValue != null && keyAliasValue != null && keyPasswordValue != null) {
                storeFile file(storeFilePath)
                storePassword storePasswordValue
                keyAlias keyAliasValue
                keyPassword keyPasswordValue
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled = true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlin {
        compilerOptions {
            jvmTarget = org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_17
        }
    }

    buildFeatures {
        buildConfig = true
        compose = true
    }
    
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

}

androidComponents {
    onVariants(selector().all()) { variant ->
        variant.outputs.each { output ->
            def buildTypeName = variant.buildType
            def versionName = rootProject.ext.appVersionName as String
            output.outputFileName.set("jellycine-${buildTypeName}-${versionName}.apk")
        }
    }
}

dependencies {
    // Project modules
    implementation project(':data')
    implementation project(':core')

    // Core Android
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.10.0'
    implementation 'androidx.activity:activity-compose:1.12.4'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.core:core-splashscreen:1.2.0'
    
    // Compose BOM
    implementation platform('androidx.compose:compose-bom:2026.01.01')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.material:material-icons-extended'
    
    // Navigation
    implementation 'androidx.navigation:navigation-compose:2.9.7'

    // ViewModel
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.10.0'

    // Image Loading with memory optimization
    implementation 'io.coil-kt.coil3:coil-compose:3.3.0'
    implementation 'io.coil-kt.coil3:coil-gif:3.3.0'
    implementation 'io.coil-kt.coil3:coil-network-okhttp:3.3.0'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2'

    // JSON
    implementation 'com.google.code.gson:gson:2.13.2'

    // Networking
    implementation 'com.squareup.okhttp3:okhttp:5.3.1'

    // DataStore
    implementation 'androidx.datastore:datastore-preferences:1.2.0'
    implementation 'androidx.profileinstaller:profileinstaller:1.4.1'

    // Media3 ExoPlayer for video playback
    implementation 'androidx.media3:media3-exoplayer:1.9.0'
    implementation 'androidx.media3:media3-exoplayer-dash:1.9.0'
    implementation 'androidx.media3:media3-exoplayer-hls:1.9.0'
    implementation 'androidx.media3:media3-exoplayer-smoothstreaming:1.9.0'
    implementation 'androidx.media3:media3-ui:1.9.0'
    implementation 'androidx.media3:media3-session:1.9.0'
    implementation 'androidx.media3:media3-common:1.9.0'
    implementation 'org.jellyfin.media3:media3-ffmpeg-decoder:1.9.0+1'

    // Audio and video processing
    implementation 'androidx.media3:media3-effect:1.9.0'

    // Networking for Jellyfin API
    implementation 'com.squareup.retrofit2:retrofit:3.0.0'
    implementation 'com.squareup.retrofit2:converter-gson:3.0.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.3.1'

    // Permissions handling
    implementation 'com.google.accompanist:accompanist-permissions:0.37.3'

    // Hilt
    implementation 'com.google.dagger:hilt-android:2.59.1'
    ksp 'com.google.dagger:hilt-compiler:2.59.1'
    implementation 'androidx.hilt:hilt-navigation-compose:1.3.0'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2026.01.01')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}
