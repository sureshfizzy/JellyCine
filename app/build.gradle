plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'com.google.dagger.hilt.android'
    id 'kotlin-kapt'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("keystore.properties")
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def signingValue = { String key, String envKey ->
    def prop = keystoreProperties.getProperty(key)
    if (prop != null && !prop.trim().isEmpty()) return prop.trim()
    def env = System.getenv(envKey)
    return (env != null && !env.trim().isEmpty()) ? env.trim() : null
}

android {
    namespace = 'com.jellycine.app'
    compileSdk = 35

    defaultConfig {
        applicationId = "com.jellycine.app"
        minSdk = 27
        targetSdk = 34
        versionCode = rootProject.ext.appVersionCode as Integer
        versionName = rootProject.ext.appVersionName as String

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary = true
        }
    }

    signingConfigs {
        release {
            def storeFilePath = signingValue("storeFile", "JELLYCINE_STORE_FILE") ?: "jellycine-release.keystore"
            def storePasswordValue = signingValue("storePassword", "JELLYCINE_STORE_PASSWORD")
            def keyAliasValue = signingValue("keyAlias", "JELLYCINE_KEY_ALIAS")
            def keyPasswordValue = signingValue("keyPassword", "JELLYCINE_KEY_PASSWORD")

            if (storePasswordValue != null && keyAliasValue != null && keyPasswordValue != null) {
                storeFile file(storeFilePath)
                storePassword storePasswordValue
                keyAlias keyAliasValue
                keyPassword keyPasswordValue
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        buildConfig = true
        compose = true
    }
    
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            def buildType = variant.buildType.name
            def versionName = variant.versionName
            outputFileName = "jellycine-${buildType}-${versionName}.apk"
        }
    }
}

dependencies {
    // Project modules
    implementation project(':data')
    implementation project(':core')

    // Core Android
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.9.2'
    implementation 'androidx.activity:activity-compose:1.9.3'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.core:core-splashscreen:1.0.1'
    
    // Compose BOM
    implementation platform('androidx.compose:compose-bom:2025.07.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.material:material-icons-extended'
    
    // Navigation
    implementation 'androidx.navigation:navigation-compose:2.9.2'

    // ViewModel
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.9.2'

    // Image Loading with memory optimization
    implementation 'io.coil-kt:coil-compose:2.7.0'
    implementation 'io.coil-kt:coil-gif:2.7.0'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.9.0'

    // JSON
    implementation 'com.google.code.gson:gson:2.11.0'

    // Networking
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'

    // DataStore
    implementation 'androidx.datastore:datastore-preferences:1.1.1'

    // Media3 ExoPlayer for video playback
    implementation 'androidx.media3:media3-exoplayer:1.8.0'
    implementation 'androidx.media3:media3-exoplayer-dash:1.8.0'
    implementation 'androidx.media3:media3-exoplayer-hls:1.8.0'
    implementation 'androidx.media3:media3-exoplayer-smoothstreaming:1.8.0'
    implementation 'androidx.media3:media3-ui:1.8.0'
    implementation 'androidx.media3:media3-session:1.8.0'
    implementation 'androidx.media3:media3-common:1.8.0'

    // Audio and video processing
    implementation 'androidx.media3:media3-effect:1.8.0'

    // Networking for Jellyfin API
    implementation 'com.squareup.retrofit2:retrofit:2.11.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.11.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'

    // Permissions handling
    implementation 'com.google.accompanist:accompanist-permissions:0.36.0'

    // Hilt
    implementation 'com.google.dagger:hilt-android:2.57'
    kapt 'com.google.dagger:hilt-compiler:2.57'
    implementation 'androidx.hilt:hilt-navigation-compose:1.3.0-alpha02'
    implementation 'androidx.hilt:hilt-lifecycle-viewmodel-compose:1.0.0-alpha03'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2025.07.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}
